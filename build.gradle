// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext.kotlin_version = "1.5.21"
    ext {
        ktx = '1.6.0'
        app_compat = '1.3.1'
        google_material = '1.4.0'
        constraint_layout = '2.0.4'
        annotation = '1.2.0'
        exif_interface = "1.1.0"
        pallete = "1.0.0"
        fragment = '1.3.6'
        swipe_refresh = '1.1.0'
        arch = "2.1.0"
        view_pager = "1.0.0"
        preference = '1.1.1'

        // Architecture
        room = '2.3.0'
        lifecycle = '2.3.1'
        paging = '3.0.1'
        navigation = '2.3.5'
        work_manager = '2.5.0'

        // Dependency Injection
        dagger = '2.38.1'
        hilt_dagger = '2.38.1'

        // Logs
        timber = "4.7.1"

        // Image
        glide = '4.12.0'
        lottie = '3.3.1'

        // Networking
        okhttp = '4.9.1'
        retrofit = '2.9.0'

        // Others
        gson = '2.8.6'

        // Build Versions
        android_gradle_plugin = '7.0.0'
        play_publisher = "3.5.0"
    }
    repositories {
        google()
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$android_gradle_plugin"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:$navigation"
        classpath "com.google.dagger:hilt-android-gradle-plugin:$hilt_dagger"
        classpath 'com.google.gms:google-services:4.3.9'
        classpath 'com.google.firebase:firebase-crashlytics-gradle:2.7.1'
    }
}

plugins {
    id 'com.diffplug.spotless' version '5.13.0'
}

allprojects {
    repositories {
        google()
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
        maven { url "https://jitpack.io" }
    }
}

subprojects {
    apply plugin: 'com.diffplug.spotless'
    spotless {
        kotlin {
            target '**/*.kt'
            targetExclude("$buildDir/**/*.kt")
            targetExclude('bin/**/*.kt')

            ktlint("0.41.0")
        }
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
        kotlinOptions {
            // Treat all Kotlin warnings as errors
            allWarningsAsErrors = true

            // Enable experimental coroutines APIs, including Flow
            freeCompilerArgs += "-Xopt-in=kotlinx.coroutines.ExperimentalCoroutinesApi"
            freeCompilerArgs += "-Xopt-in=kotlinx.coroutines.FlowPreview"
            freeCompilerArgs += "-Xopt-in=kotlin.Experimental"

            // Set JVM target to 1.8
            jvmTarget = "1.8"
        }
    }
}

task cleanEverything(type: Delete) {
    delete rootProject.buildDir
}