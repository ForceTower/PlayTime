import java.nio.file.Files

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-parcelize'
apply plugin: 'dagger.hilt.android.plugin'
apply plugin: 'androidx.navigation.safeargs'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "dev.forcetower.playtime"
        minSdkVersion 23
        targetSdkVersion 30
        versionCode 2
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += [
                    "room.schemaLocation": "$projectDir/schemas".toString()
                ]
            }
        }

        ndkVersion "21.3.6528147"
    }

    signingConfigs {
        release {
            def password = System.getenv("APP_KEYSTORE_PASSWORD")
            if (password == null)
                password = "android"

            def alias = System.getenv("APP_KEYSTORE_ALIAS")
            if (alias == null)
                alias = "androiddebugkey"

            def keyPass = System.getenv("APP_KEYSTORE_PRIVATE_KEY_PASSWORD")
            if (keyPass == null)
                keyPass = "android"

            def signFile = rootProject.file("sign.jks")
            if (!signFile.exists())
                signFile = rootProject.file("debug.keystore")

            storeFile signFile
            storePassword password
            keyAlias alias
            keyPassword keyPass
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            shrinkResources false
            manifestPlaceholders = [crashlyticsEnabled: true]
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            manifestPlaceholders = [crashlyticsEnabled: false]
            applicationIdSuffix ".debug"
        }
    }

    buildFeatures {
        dataBinding true
    }

    kapt {
        correctErrorTypes true
        javacOptions {
            option("-Xmaxerrs", 1000)
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
    }
}

dependencies {
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation project(':toolkit')

    // Android X
    implementation "androidx.core:core-ktx:$ktx"
    implementation "androidx.annotation:annotation:$annotation"
    implementation "androidx.appcompat:appcompat:$app_compat"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:$swipe_refresh"
    implementation "androidx.constraintlayout:constraintlayout:$constraint_layout"
    implementation "com.google.android.material:material:$google_material"
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation "androidx.palette:palette-ktx:$pallete"
    implementation "androidx.fragment:fragment-ktx:$fragment"
    implementation "androidx.viewpager2:viewpager2:$view_pager"
    implementation "androidx.preference:preference-ktx:$preference"

    // Architecture
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle"

    implementation "androidx.navigation:navigation-ui-ktx:$navigation"
    implementation "androidx.navigation:navigation-fragment-ktx:$navigation"

    implementation "androidx.work:work-runtime-ktx:$work_manager"

    // Paging
    implementation "androidx.paging:paging-runtime-ktx:$paging"

    // Dependency injection
    implementation "com.google.dagger:hilt-android:$hilt_dagger"
    kapt "com.google.dagger:hilt-android-compiler:$hilt_dagger"
    implementation 'androidx.hilt:hilt-work:1.0.0'
    kapt 'androidx.hilt:hilt-compiler:1.0.0'

    // Room
    implementation "androidx.room:room-runtime:$room"
    implementation "androidx.room:room-ktx:$room"
    kapt "androidx.room:room-compiler:$room"

    // Logs
    implementation "com.jakewharton.timber:timber:$timber"

    // Networking
    implementation "com.squareup.okhttp3:okhttp:$okhttp"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp"
    implementation "com.squareup.retrofit2:retrofit:$retrofit"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit"

    // Image
    implementation "com.github.bumptech.glide:glide:$glide"
    kapt "com.github.bumptech.glide:compiler:$glide"

    // YouTube
    implementation 'com.pierfrancescosoffritti.androidyoutubeplayer:core:10.0.5'

    // Firebase
    implementation platform('com.google.firebase:firebase-bom:28.1.0')
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-crashlytics-ktx'

    implementation 'com.eightbitlab:blurview:1.6.6'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}

def googleServices = file("google-services.json")
if (!googleServices.exists()) {
    Files.copy(file("fake-google-services.json").toPath(), googleServices.toPath())
}